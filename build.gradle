plugins {
    id 'checkstyle'
    id 'application'
    id 'java'
    id 'jacoco'
    id 'com.diffplug.gradle.spotless' version '3.24.2'
    id 'org.openjfx.javafxplugin' version '0.0.9'
}

sourceCompatibility = '1.14'
targetCompatibility = '1.14'
mainClassName = 'edu.wpi.surflab.CurvatureApp'

//TestFX doesn't play nice with macOS
String osName = System.getProperty("os.name").toLowerCase();
if (osName.contains("mac")) {
    project.setProperty("headless", "true")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(
            'org.controlsfx:controlsfx:8.40.16',
            'com.panemu:tiwulfx:2.0',
            'junit:junit:4.12',
            'org.jetbrains:annotations:15.0',
    )

    compileOnly(
            'org.projectlombok:lombok:1.18.8',
    )

    annotationProcessor(
            'org.projectlombok:lombok:1.18.8'
    )

    testImplementation(
            'org.junit.jupiter:junit-jupiter:5.5.1',
            'org.testfx:testfx-junit5:4.0.16-alpha',
    )
    testRuntimeOnly(
            "org.testfx:openjfx-monocle:8u76-b04",
    )
}

javafx {
    version = "14"
    modules = [
        'javafx.fxml',
        'javafx.base',
        'javafx.controls',
    ]
}

test {
    useJUnitPlatform()
    jvmArgs "-Dheadless=${project.hasProperty('headless') ? project.headless : false}"
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
            excludes = [
                    'edu.wpi.cs3733.c20.teamV.App',
                    'edu.wpi.cs3733.c20.teamV.Main'
            ]
        }

        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.25
            }
            excludes = [
                    'edu.wpi.cs3733.c20.teamV.App',
                    'edu.wpi.cs3733.c20.teamV.Main'
            ]
        }

        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.25
            }
            excludes = [
                    'edu.wpi.cs3733.c20.teamV.App',
                    'edu.wpi.cs3733.c20.teamV.Main'
            ]
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn jacocoTestReport

spotless {
    java {
        googleJavaFormat()
    }
}
